/*
 * Licensed to the Apache Software Foundation (ASF) under one or more contributor license agreements. See the NOTICE
 * file distributed with this work for additional information regarding copyright ownership. The ASF licenses this file
 * to You under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
description = 'Key value store implementations.'

task openJPAEnhance {
  description "Enhance JPA model classes using OpenJPA Enhancer"
  dependsOn compileTestJava

  doLast {
    // define the entity classes
    def entityFiles = sourceSets.test.output.asFileTree.filter { it.name == "Store.class" }

    println "Enhancing with OpenJPA, the following files..."
    entityFiles.getFiles().each { println it }

    // define Ant task for Enhancer
    ant.taskdef(
      name : 'openjpac',
      classpath : sourceSets.test.runtimeClasspath.asPath,
      classname : 'org.apache.openjpa.ant.PCEnhancerTask'
      )

    // Run the OpenJPA Enhancer as an Ant task
    //   - see OpenJPA 'PCEnhancerTask' for supported arguments
    //   - this invocation of the enhancer adds support for a default-ctor
    //   - as well as ensuring JPA property use is valid.
    ant.openjpac(
      classpath: sourceSets.test.runtimeClasspath.asPath,
      addDefaultConstructor: true,
      enforcePropertyRestrictions: true) {
        entityFiles.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
      }
  }
}
test { dependsOn openJPAEnhance }

dependencies {

  compile project(':bytes')
  compile project(':concurrent-coroutines')
  compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
  compile 'org.jetbrains.kotlinx:kotlinx-coroutines-guava'
  compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'
  compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  compile 'javax.persistence:javax.persistence-api'
  compileOnly 'com.jolbox:bonecp'
  compileOnly 'io.lettuce:lettuce-core'
  compileOnly 'org.fusesource.leveldbjni:leveldbjni-all'
  compileOnly 'org.infinispan:infinispan-core'
  compileOnly 'org.mapdb:mapdb'
  compileOnly 'org.rocksdb:rocksdbjni'

  testCompile project(':concurrent')
  testCompile project(':junit')
  testCompile 'com.jolbox:bonecp'
  testCompile 'com.github.kstyrc:embedded-redis'
  testCompile 'com.h2database:h2'
  testCompile 'com.winterbe:expekt'
  testCompile 'io.lettuce:lettuce-core'
  testCompile 'org.fusesource.leveldbjni:leveldbjni-all'
  testCompile  'org.infinispan:infinispan-core'
  testCompile 'org.jetbrains.spek:spek-api'
  testCompile 'org.junit.jupiter:junit-jupiter-api'
  testCompile 'org.junit.jupiter:junit-jupiter-params'
  testCompile 'org.mapdb:mapdb'
  testCompile 'org.rocksdb:rocksdbjni'
  testRuntime 'org.apache.openjpa:openjpa'


  testRuntime 'org.jetbrains.spek:spek-junit-platform-engine'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine'
}
